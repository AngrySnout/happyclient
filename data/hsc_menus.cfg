newgui main [
    guilist [
        guitext "name: " (playermodelicon)
        newname = (getname)
        guifield newname 15 [name $newname]
    ]
    guibutton (concat "model:" (playermodelname)) [guirolloveraction = (playermodelbutton $playermodel); showgui playermodel] (playermodelicon)
    guilist [
        guibutton "crosshair: " [showgui crosshair] (playermodelicon)
        guiimage (getcrosshair) [showgui crosshair] 0.5
    ]
    guibar
    guibutton "server browser.."  "showgui servers"
    if (isconnected) [
        guibar
        if (|| $editing (m_edit (getmode))) [
            guibutton "editing.." "showgui editing"
        ]
        guibutton "vote game mode / map.." "showgui gamemode"
        // guibutton "lanconnect"
        // guibutton "connect localhost"
        guibutton "switch team" [if (strcmp (getteam) "good") [team evil] [team good]]
        guibutton "toggle spectator" [spectator (! (isspectator (getclientnum)))] "spectator"
        guibutton "master.." [showgui master]
        guibar
        //guibutton "demos.." [showgui demobrowser]
        guibutton "options.."  "showgui options"
        guibutton "new options.."        "showgui newoptions"       
        guibutton "disconnect" "disconnect"         "exit"
    ] [
        guibutton "bot match.."      "showgui botmatch"
        guibutton "campaign.."       "showgui campaign"
        guibar
        //guibutton "demos.." [showgui demobrowser]
        guibutton "options.."        "showgui options"
        guibutton "new options.."    "showgui newoptions"       
        guibutton "about.."          "showgui about"
        guibutton "quit"             "quit"                 "exit"
    ]
] 0

newgui master [
  guistayopen [
    if (ismaster (getclientnum)) [
        guibutton "relinquish master" "setmaster 0"
    ] [
        guibutton "claim master" "setmaster 1"
    ]
    guilist [
        guitext "mastermode: "
        mm = (getmastermode)
        guiradio "open (0) " mm 0 "mastermode 0"
        guiradio "veto (1) " mm 1 "mastermode 1"
        guiradio "locked (2) " mm 2 "mastermode 2"
        guiradio "private (3) " mm 3 "mastermode 3"
    ]
    guibar
    guilist [
        guilist [
            looplist cn (listclients 0 0) [
                guitext (concatword (getclientname $cn) "(" $cn "): ") (getclienticon $cn)
            ]
        ]
        guilist [
            looplist cn (listclients 0 0) [
                guibutton "toggle spectator " [spectator @(= (isspectator $cn) 0) @cn]
            ]
        ]
        guilist [
                looplist cn (listclients 0 0) [
                if (m_teammode (getmode)) [
                    guibutton "switch team " [setteam @cn @(? (=s (getclientteam $cn) "good") "evil" "good")]
                ]
            ]
        ]
        guilist [
            looplist cn (listclients 0 0) [
                if (ismaster $cn) [
                    guibutton "take master" [setmaster 0 @cn]
                ] [
                    guibutton "give master" [setmaster 1 @cn]
                ]
            ]
        ]
        guilist [
            guitext "    " 0
        ]
        guilist [
            looplist cn (listclients 0 0) [
                guibutton "kick " [kick @cn]
            ]
        ]
        guilist [
            looplist cn (listclients 0 0) [
                guitext (getclientip $cn " ") 0
            ]
        ]
        guilist [
            looplist cn (listclients 0 0) [
                guibutton "whois " [whois @cn]
            ]
        ]
        guilist [
            looplist cn (listclients 0 0) [
                guibutton "add friend " [addfriend @cn]
            ]
        ]
    ]
  ]
]

newgui demobrowser [
    guilist [
        guilist [
            guibutton "ffa"                "initdemos ffa; showgui demos"
            guibutton "teamplay"           "initdemos teamplay; showgui demos"
            guibutton "instagib"           "initdemos instagib; showgui demos"
            guibutton "instagib team"      "initdemos instagib_team; showgui demos"
            guibutton "efficiency"         "initdemos efficiency; showgui demos"
            guibutton "efficiency team"    "initdemos efficiency_team; showgui demos"
            guibutton "tactics"            "initdemos tactics; showgui demos"
            guibutton "tactics team"       "initdemos tactics_team; showgui demos"
            guibutton "capture"            "initdemos capture; showgui demos"
            guibutton "regen capture"      "initdemos regen_capture; showgui demos"
        ]
        guibar
        guilist [
            guibutton "ctf"                "initdemos ctf; showgui demos"
            guibutton "insta ctf"          "initdemos insta_ctf; showgui demos"
            guibutton "efficiency ctf"     "initdemos efficiency_ctf; showgui demos"
            guibutton "protect"            "initdemos protect; showgui demos"
            guibutton "insta protect"      "initdemos insta_protect; showgui demos"
            guibutton "efficiency protect" "initdemos efficiency_protect; showgui demos"
            guibutton "hold"               "initdemos hold; showgui demos"
            guibutton "insta hold"         "initdemos insta_hold; showgui demos"
            guibutton "efficiency hold"    "initdemos efficiency_hold; showgui demos"
            guibutton "collect"            "initdemos collect; showgui demos"
            guibutton "insta collect"      "initdemos insta_collect; showgui demos"
            guibutton "efficiency collect" "initdemos efficiency_collect; showgui demos"
        ]
    ]
]

newgui demos [
    guidemos [
        guitext (concat "mode:" $demolist) 0
        guibar
        guitext "max: "
        newmaxnodemos = $maxnodemos
        guifield newmaxnodemos 12 [maxnodemos $newmaxnodemos]
    ] 17 
]

newgui servers [
    guistayopen [
        guilist [
            guibutton "update from master server" [
                updatefrommaster
                permaservers
            ]
            guibar
            guicheckbox "search LAN" searchlan
            guibar
            guicheckbox "auto-update" autoupdateservers
            guibar
            guicheckbox "auto-sort" autosortservers
            if (= $autosortservers 0) [
                guibar
                guibutton "sort" "sortservers"
            ]
            guibar
            guicheckbox "show filters" showserverfilters
            guibar
            guicheckbox "show quick servers" showquickservers
        ]
        if (= (getalias showserverfilters) 1) [
            guibar
            guilist [
                guitext "description filter:  "
                newfilterdesc = $filterdesc
                guifield newfilterdesc 12 [filterdesc $newfilterdesc]
                guibar
                guitext "map filter:  "
                newfiltermap = $filtermap
                guifield newfiltermap 12 [filtermap $newfiltermap]
                guibar
                guibutton "more..." "showgui serverfilters"
            ]
        ]
        if (= (getalias showquickservers) 1) [
            guibar
            guilist [
                quickserv "PSL 1" "85.214.66.181" 10000
                quickserv "PSL 2 " "85.214.66.181" 20000
                quickserv "PSL 3 " "85.214.66.181" 30000
                quickserv "PSL 4 " "85.214.66.181" 40000
                guibar
                quickserv "[happy] Clan Server " "192.95.30.66" 28790
                quickserv "Happy Happy " "162.246.23.139" 28785
            ]
        ]
        guibar
        guilist [
            guibutton "players..." [playerbrowsertype 0; showgui findplayers]
            guibar
            guibutton "friends.." [playerbrowsertype 1; showgui findplayers]
            //guibar
            //guibutton "clans.." [playerbrowsertype 2; showgui findplayers]
            guibar
            guicheckbox "server preview" serverpreview
        ]
        guibar
        guiservers "" 0 16
    ]
] "" [initservers]

quickserv = [
    guibutton (quickserver (concatword $arg1 " ") $arg2 $arg3) (concat "connect" $arg2 $arg3)
]

permaservers = [
    addserver 85.214.66.181 20000
    addserver 85.214.66.181 30000
    addserver 85.214.66.181 40000
]
permaservers

newgui findplayers [
    guiplayers [
        guistayopen [
            guilist [
                guitext "filter:  "
                newfiltername = $filtername
                guifield newfiltername 15 [filtername $newfiltername]
                guibar
                guitext "refresh interval:  "
                newrefreshinterval = $playerrefreshinterval
                guifield newrefreshinterval 15 [playerrefreshinterval $newrefreshinterval]
                guibar
                guibutton "refresh" [refreshextplayers]
                guibar
                guitext (concat (extplayerstotal) "online")
            ]
            guibar
        ]
    ] 17
] "players"

newgui serverfilters [
    guilist [
        guilist [
            guitext "hide" 0
            guicheckbox "full" filterfull 1 0
            guicheckbox "empty" filterempty 1 0
            guicheckbox "unknown" filterunknown 1 0
        ]
        guibar
        guilist [
            guitext "hide >=" 0
            guiradio "locked" filtermmode 1
            guiradio "private" filtermmode 2
            guiradio "password" filtermmode 3
            guiradio "none" filtermmode 0
        ]
    ]
] "serverfilters"

quicksay = 0

newgui newoptions [
    guialign 0 [ guilist [
        guititle "^fOscoreboard items"
        guistrut 1
        guitext "flag holder, flags scored, and frags"
        guitext "do not require ext info to be enabled"
        guistrut 1
        guilist [
            guilist [
                guitext "scoreboard:" 0
                guicheckbox "show ext info" scoreboardextinfo
                guicheckbox "show flag holder" scoreboardflag
                guicheckbox "show flags scored" scoreboardflags
                guicheckbox "show frags" scoreboardfrags
                guicheckbox "show deaths" scoreboarddeaths
                guicheckbox "show accuracy" scoreboardaccuracy
            ]
            guibar
            guilist [
                guitext "player country:" 0
                guiradio "off" scoreboardcountry 0
                guiradio "flag" scoreboardcountry 1
                guiradio "code" scoreboardcountry 2
                guiradio "flag & code" scoreboardcountry 3
                guiradio "name" scoreboardcountry 4
                guiradio "flag & name" scoreboardcountry 5
            ]
        ]
        guicheckbox "show kpd" scoreboardkpd
        guicheckbox "show teamkills" scoreboardteamkills
        guicheckbox "show spectators' pings" scoreboardspecping
        guistrut 1
    ] ]
    
    guitab "autosay"
    guititle "^fOauto sorry/np/gg"
    guistrut 1
    guicheckbox "say sorry and np in team chat" autosayteam
    guitext "use %s in place of the player's name"
    guistrut 1
    guilist [
        guilist [
            guicheckbox "enable auto sorry" doautosorry
            guitext "auto sorry message: " 0
            guistrut 1
            guicheckbox "enable auto np" doautonp
            guitext "auto np message: " 0
            guistrut 1
            guicheckbox "enable auto good game" doautogg
            guitext "auto good game message: " 0
        ]
        guilist [
            guistrut 1
            newautosorry = $autosorry
            guifield newautosorry 40 [autosorry $newautosorry]
            guistrut 1
            guistrut 1
            newautonp = $autonp
            guifield newautonp 40 [autonp $newautonp]
            guistrut 1
            guistrut 1
            newautogg = $autogg
            guifield newautogg 40 [autogg $newautogg]
        ]
    ]
    
    guitab misc
    guialign 0 [ guilist [
        guititle "^fOweapons"
        guistrut 1
        guicheckbox "rifle shots are lightning" rifletraillightning
        guilist [
            guitext "highlight ammo when less than " 0
            newammohighlight = $ammohighlight
            guifield newammohighlight 2 [ammohighlight $newammohighlight]
        ]
        guistrut 1
        guibar
        guititle "^fOwhois"
        guistrut 1
        guicheckbox "enable whois player tracking" whoisenabled
        guicheckbox "print whois msg on connect" globalwhois
        guistrut 1
        guibar
        guititle "^fOscreenshots"
        guistrut 1
        guilist [
            guilist [
                guitext "screenshot directory " 0
                guitext "screenshot format " 0
            ]
            guilist [
                newscreenshotdir = $screenshotdir
                guifield newscreenshotdir 20 [screenshotdir $newscreenshotdir]
                guilist [
                    guiradio "BMP" screenshotformat 0
                    guibar
                    guiradio "TGA" screenshotformat 1
                    guibar
                    guiradio "PNG" screenshotformat 2
                ]
            ]
        ]
    ] ]
    
    guitab "misc 2"
    guialign 0 [ guilist [
        guititle "^fOchat highlighting"
        guistrut 1
        guicheckbox "enable chat word highlighting" chathighligh
        guistrut 1
        guilist [
            guilist [
                guitext "highlight the words:" 0
                guitext "highlight style:" 0
            ]
            guilist [
                newhighlightwords = $highlightwords
                guifield newhighlightwords 40 [highlightwords $newhighlightwords]
                guilist [
                    guiradio "red" highlightstyle 0
                    guistrut 1
                    guiradio "orange" highlightstyle 1
                    guistrut 1
                    guiradio "blinking" highlightstyle 2
                    guistrut 1
                    guiradio "circus" highlightstyle 3
                ]
            ]
        ]
        
        guistrut 1
        guibar
        guititle "^fOfrag console"
        guistrut 1
        guicheckbox "enable frag console" fragconsole
        guistrut 1
        guitext "frag console filter:" 0
        guibitfield "self frags" fragconfilter 0x800
        guibitfield "other player frags" fragconfilter 0x1000
        guibitfield "team kills" fragconfilter 0x2000
    ] ]
    
    guitab quickchat
    guialign 0 [ guilist [
        guititle "^fOquick team chat"
        guistrut 1
        if (!= (getalias quickchatinit) 1) [
            quicksay1 = "<INCOMING>"
            quicksay2 = "<CLEAR BASE>"
            quicksay3 = "<INCOMING LEFT>"
            quicksay4 = "<INCOMING RIGHT>"
            quicksay5 = "<HE'S LOW>"
            quicksay6 = "<I'M LOW>"
            quicksay7 = "<DROP THE FLAG FOR A SECOND AND PICK IT UP AGAIN>"
            quicksay8 = "<HELP>"
            quicksay9 = "<GOT HIM>"
            quicksay10 = "<CAMPER>"
            //bind LCTRL [ quicksay ]
            quickchatinit = 1
        ]
        guilist [
            guitext "modifier key: " 0
            guikeyfield newkey -9 [bind $newkey [quicksay]]
        ]
        guistrut 1
        guilist [
            guitext "1.  " 0
            guifield quicksay1 50
        ]
        guilist [
            guitext "2.  " 0
            guifield quicksay2 50
        ]
        guilist [
            guitext "3.  " 0
            guifield quicksay3 50
        ]
        guilist [
            guitext "4.  " 0
            guifield quicksay4 50
        ]
        guilist [
            guitext "5.  " 0
            guifield quicksay5 50
        ]
        guilist [
            guitext "6.  " 0
            guifield quicksay6 50
        ]
        guilist [
            guitext "7.  " 0
            guifield quicksay7 50
        ]
        guilist [
            guitext "8.  " 0
            guifield quicksay8 50
        ]
        guilist [
            guitext "9.  " 0
            guifield quicksay9 50
        ]
        guilist [
            guitext "0.  " 0
            guifield quicksay10 50
        ]
    ] ]
        
    quicksay = [
        oldbind1 = (getbind 1); oldbind2 = (getbind 2); oldbind3 = (getbind 3)
        oldbind4 = (getbind 4); oldbind5 = (getbind 5); oldbind6 = (getbind 6)
        oldbind7 = (getbind 7); oldbind8 = (getbind 8); oldbind9 = (getbind 9)
        oldbind0 = (getbind 0)
        bind 1 [sayteam $quicksay1]; bind 2 [sayteam $quicksay2]; bind 3 [sayteam $quicksay3]
        bind 4 [sayteam $quicksay4]; bind 5 [sayteam $quicksay5]; bind 6 [sayteam $quicksay6]
        bind 7 [sayteam $quicksay7]; bind 8 [sayteam $quicksay8]; bind 9 [sayteam $quicksay9]
        bind 0 [sayteam $quicksay10]
        quicksaying = 1
        onrelease [
            bind 1 $oldbind1; bind 2 $oldbind2; bind 3 $oldbind3
            bind 4 $oldbind4; bind 5 $oldbind5; bind 6 $oldbind6
            bind 7 $oldbind7; bind 8 $oldbind8; bind 9 $oldbind9
            bind 0 $oldbind0
            quicksaying = 0
        ]
    ]
    
    guitab irc
    guialign 0 [ guilist [
        guititle "^fOin-game IRC"
        guistrut 1
        guistayopen [
            guilist [
                guilist [
                    guitext "network         "
                    guitext "address         "
                    guitext "port            "
                    guitext "nickname        "
                    guitext "channel         "
                    guitext "ignore list     "
                    guitext "quit msg        "
                    guitext "highlight words "
                    guitext "highlight style "
                ]
                guilist [
                    guifield ircname_ -40
                    guifield irchost_ -40
                    guifield ircport_ -40
                    guifield ircnick_ -40
                    guifield ircchan_ -40
                    guifield ircignore -40
                    guifield ircquitmsg -40
                    guifield irchighlightwords -40
                    guilist [
                        guiradio "red " irchighlightstyle 0
                        guiradio "orange " irchighlightstyle 1
                        guiradio "blinking " irchighlightstyle 2
                        guiradio "circus " irchighlightstyle 3
                    ]
                ]
            ]
            guistrut 1
            guicheckbox "show messages in chat console" ircinchat
            guicheckbox "autoconnect on start up" ircautoconn
            guistrut 1
            guilist [
                guibutton "#happy channel" [
                    ircname_ "GameSurge"
                    irchost_ "irc.gamesurge.net"
                    ircport_ 6667
                    if (=s $ircnick_ "") [
                        ircnick_ (getname)
                    ]
                    ircchan_ "#happy"
                    ircignore_ "happybot"
                ]
                guistrut 3
                guibutton "connect" [
                    ircdisconnect
                    ircaddclient $ircname_ $irchost_ $ircport_ $ircnick_
                    ircaddchan $ircname_ $ircchan_
                ]
                guistrut 3
                guibutton "disconnect" [ircdisconnect]
                guistrut 3
                guibutton "join" [ ircaddchan $ircname_ $ircchan_ ]
            ]
        ]
    ] ]
    
    guitab stats
    guialign 0 [ guilist [
        guititle "^fOstatistics"
        guistrut 1
        guicheckbox "record stats" recordstats
        guistrut 1
        guilist [
            guilist [
                guilist [
                    guilist [
                        guitext "^fifrags: " 0
                        guitext "^fishots: " 0
                        guitext "^fideaths: " 0
                        guitext "^fikpd: " 0
                        guitext "^fiflags scored: " 0
                    ]
                    guilist [
                        guitext $stats_frags 0
                        guitext $stats_shots 0
                        guitext $stats_deaths 0
                        guitext (divf $stats_kpd 100.0) 0
                        guitext $stats_flags 0
                    ]
                ]
            ]
            guibar
            guilist [
                guilist [
                    guilist [
                        guitext "^fiaccuracy: " 0
                        guitext "^fiteamkills: " 0
                        guitext "^figot teamkilled: " 0
                        guitext "^fisuicides: " 0
                        guitext "^fidamage: " 0
                    ]
                    guilist [
                        guitext (concatword $stats_accuracy "%") 0
                        guitext $stats_teamkills 0
                        guitext $stats_got_teamkilled 0
                        guitext $stats_suicides 0
                        guitext $stats_damage 0
                    ]
                ]
            ]
        ]
        guistrut 1
        guilist [
            guitext "^fitime played: " 0
            guitext (concatword (div $stats_time 3600) ":" (mod (div $stats_time 60) 60) ":" (mod $stats_time 60)) 0
        ]
        guistrut 1
        guistayopen [
            guibutton "reset all" [
                stats_frags 0
                stats_shots 0
                stats_deaths 0
                stats_kpd 0
                stats_flags 0
                stats_accuracy 0
                stats_teamkills 0
                stats_got_teamkilled 0
                stats_suicides 0
                stats_damage 0
                stats_time 0
            ]
        ]
    ] ]
    
] "scoreboard"

gamehud = [if (!= $quicksaying 1) [result ""] [result (concat "^f31.^f0" $quicksay1 "^n^f32.^f0" $quicksay2 "^n^f33.^f0" $quicksay3 "^n^f34.^f0" $quicksay4 "^n^f35.^f0" $quicksay5 "^n^f36.^f0" $quicksay6 "^n^f37.^f0" $quicksay7 "^n^f38.^f0" $quicksay8 "^n^f39.^f0" $quicksay9 "^n^f310.^f0" $quicksay10)]]

sayirccommand = [inputcommand (if (>= $numargs 1) [result $arg1] [result ""]) [sayirc $ircname_ $ircchan_ $commandbuf] "[irc]"]
bind u sayirccommand

newgui extscoreboard [
    guinoautotab [
        guiextscoreboard
        guibar
        guibutton "connect" [connectextselected]
    ]
]
